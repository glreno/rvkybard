        .TITLE      WATERMARKED MEMO PAD
;
; All this thing does is boot up,
; set up a custom Display List,
; create a few PM graphics,
; and then JMP $E471,
; which is the built-in ATARI COMPUTER MEMO PAD application.
; This cartridge does NOT work on XL/XE computers!
;
; To load into MAC65: ENTER #D:WM.LST,A
;
        .PAGE       SETUP
        .OPT OBJ
; Pick one of: 0=Cassette
;              1=Disk
;              2=2KB Cartridge
;              4=4KB Cartridge
;              8=8KB Cartridge
; (this is Line 200)
TARGET  = 1
;
        .PAGE       Addresses
; Where the assembled code goes!
        .IF TARGET = 8
STARTAP =   $A000 ; 8K left cartridge
        .ENDIF
        .IF TARGET = 4
STARTAP =   $B000 ; 4K left cartridge
        .ENDIF
        .IF TARGET = 2
STARTAP =   $B800 ; 2K left cartridge
        .ENDIF
        .IF TARGET = 2 .OR TARGET = 4 .OR TARGET = 8 ; any size cartridge
STARTPM =   $B800 ; 2K before end of cart
CARTINI =   $BFF9 ; end of left cartridge
        .ENDIF
;
        .IF TARGET = 1 ; disk
; disk start vector ; this is line 400
STARTAP =   $2800 ; just above memlo after DOS loads
STARTPM =   $2800
RUNAD   = $02E0
        *= RUNAD
        .WORD STARTUP ; uncomment if building for disk
        .ENDIF

;
; OS address constants
;
BLKBDV  =   $E471
NOCLIK  =   $02DB ; alas, XL only
PMBASE  =   $D407
GRACTL  =   $D01D
SDMCTL  =   $022F
GPRIOR  =   $026F
HPOSP0  =   $D000
HPOSP1  =   $D001
HPOSP2  =   $D002
HPOSP3  =   $D003
HPOSM0  =   $D004
HPOSM1  =   $D005
HPOSM2  =   $D006
HPOSM3  =   $D007
SIZEP0  =   $D008
SIZEP1  =   $D009
SIZEP2  =   $D00A
SIZEP3  =   $D00B
SIZEM   =   $D00C
COLPM0  =   $02C0
COLPM1  =   $02C1
COLPM2  =   $02C2
COLPM3  =   $02C3
WSYNC   =   $D40A
VCOUNT  =   $D40B
VDLSTL  =   $0200 ; vector for display list interrupt
VDLSTH  =   $0201
SDLSTL  =   $0230 ; location of display list
SDLSTH  =   $0231
NMIEN   =   $D40E ; 0x80=DLI 0x40=VBI 0x20=RESET, default is 0x40
;
        .PAGE       Some handy macros
;
; @LC -- load a constant byte into an address
        .MACRO @LC
            LDA # %1
            STA %2
        .ENDM
;
; Put %1 copies of %2 into a .BYTE op
; "@B 4,$FF" instead of ".BYTE $FF $FF $FF $FF"
        .MACRO @B
            .IF %1 > 0
                .BYTE %2
                @B %1-1,%2
            .ENDIF
        .ENDM
;
;
        .PAGE       Application Startup
        *=  STARTAP
STARTUP ; @LC 1,NOCLIK ; Uncomment to turn of key click
CR_DL   @LC 0,SDMCTL    ; turn off ANTIC while updating dlist
        ; We're just going to modify the dlist in place
        ; copy dlist addr to page zero $CC
        LDA SDLSTL
        STA $CC
        LDA SDLSTH
        STA $CD
        LDA #$82
        LDY #6      ; interrupt on text line 1
        STA ($CC),Y
        LDY #20     ; interrupt on text line 15
        STA ($CC),Y
EN_DL   LDA #<DLI
        STA VDLSTL
        LDA #>DLI
        STA VDLSTH
EN_PM   LDA #>STARTPM
        STA PMBASE
        @LC 3,GRACTL
        @LC 1,GPRIOR
        ; after dlist and pm are set up, enable Interrupts and ANTIC
        LDA #$C0
        STA NMIEN
        @LC $2E,SDMCTL  ; 2(norm playfield)+4(missiles)+8(players)+32(enable ANTIC)
; SET SIZES     $1C=00 01 11 00 -> 01=double-wide, 11=quad-wide
;       LDA #0
;       STA SIZEP0  ; handled in DLI
;       STA SIZEP3  ; handled in DLI
        LDA #1
        STA SIZEP1
        STA SIZEP2
        @LC $1C,SIZEM
; SET COLORS
        LDA #$92 ; DARK BLUE for the watermark (1 step less than the default bkgd $94)
        ; LDA #$2C ; ORANGE for P0
        STA COLPM0
        ; LDA #$5C ; PINK for P1
        STA COLPM1
        ; LDA #$8C ; BLUE for P2
        STA COLPM2
        ; LDA #$BC ; GREEN for P3
        STA COLPM3
; Move them all in to place
;       @LC $6C,HPOSP0  ; handled in DLI
        @LC $5C,HPOSP1
;       @LC $88,HPOSP3  ; handled in DLI
        @LC $90,HPOSP2
        @LC $8E,HPOSM0 ; HPOSP3 + 6px orange
        @LC $7C,HPOSM1 ; was 80 pink
        @LC $78,HPOSM2 ; was 7C blue
        @LC $6C,HPOSM3 ; HPOSP0
        JMP BLKBDV  ; start memopad
;
;
;

        .PAGE       Display List Interrupt
;
;
DLI     PHA ; always save the accumulator!
        LDA VCOUNT
        CMP #79      ; 16th line
        BEQ ROW80
        ;
        ; Above logo -- set all positions and sizes
ROW24   ;@LC $18,$0618
        @LC $6C,HPOSP0
        @LC $88,HPOSP3
        LDA #0
        STA SIZEP0
        STA SIZEP3
        ; STA WSYNC ; no need to sync, all are invisible
        PLA  ; restore accumulator
        RTI
        ; top of logo done - change P0,P3 sizes, and shift to new positions
ROW80   ;@LC $5C,$065C
        LDA #3
        STA SIZEP0
        STA SIZEP3
        @LC $3C,HPOSP0 ; HPOSP1-32
        @LC $A0,HPOSP3 ; HPOSP2+16
        ;STA WSYNC ; no need for sync, P0 and P3 are empty on this line
        PLA  ; restore accumulator
        RTI
;
;
        .PAGE       PM Image data
;
;
;
        *=  STARTPM   ; START OF PM DATA
        *=  STARTPM+$180   ; START OF MISSILES
        ; Missiles are stored M3 M2 M1 M0
        ;          bit flags: C0 30 0C 03
        ; Missile 0 is on the RIGHT side of the screen with Player 3
        ; Missile 3 is on the LEFT side of the screen with Player 0
        ; Missiles 1&2 are in the center; 2 is quad size.
        ; The first four bytes (11,22,33,44) are off screen
        ;
        ;
M       @B 16,0                    ; above E:
        @B 16,0                    ; Each 16 bytes is 4 chars high; 5 char margin above image
        @B 4,0
        ; Vertical stripe on M2 M1: $3C = 00 11 11 00
        @B 12,$3C
        @B 16,$3C
        @B 16,$3C
        @B 2,$FF ; 1111 1111
        @B 4,$BD ; 1011 1101
        @B 9,$3C
        @B 1,0
        @B 16,$0                   ; 5 char margin to the end of E:
        @B 16,0                    ; below E:
        ; The last four bytes (11,22,33,44) are off screen
        *=  STARTPM+$200   ; START OF PLAYER 0
P0      @B 16,$0     ; above E:
        ;.BYTE 0,$FF,0,$7E, $81,$81,$81,$81, $81,$7E,0,$FF, 0,0,0,0 ; ZERO
        @B 16,0     ; margin
        @B 4,0      ; last of 5 char margin
                    ; 4.5 chars of vertical line 4px wide
        @B 16,$0F
        @B 2,$0F
                    ; then start shifting over; 5px wide for 1 char, 6px wide for 1 char
        @B 4,$1F
        @B 4,$3F
                    ; stay at 6px wide, and shift over for 1 char, then shift over for one char
        @B 4,$7E
        @B 4,$FC
                    ; this is the point where Player1 starts
        @B 4,$F8
        @B 4,$F0
        @B 2,$C0
        @B 4,$00   ; blank space while we switch sides and switch to size 3(8px)
        @B 1,$01
        @B 1,$03
        @B 8,$0F
        @B 1,$0C
        @B 1,0
        @B 16,0
        @B 16,0       ; below E:
        *=  STARTPM+$280   ; START OF PLAYER 1
P1      @B 16,$0    ; above E:
        ;.BYTE 0,$FF,0,$18, $38,$58,$18,$18, $18,$18,0,$FF, 0,0,0,0 ; ONE
        @B 16,$0
        @B 16,$0
        @B 16,$0    ; that's all in player 0
        @B 6,$0
        @B 4,$01
        @B 2,$03
        @B 2,$07
        @B 2,$0F
        @B 2,$1F
        @B 1,$3F
        @B 3,$FF
        @B 4,$FE
        @B 2,$F8
        @B 4,0
        @B 16,0
        @B 16,0     ; below E:
        *=  STARTPM+$300  ; START OF PLAYER 2
P2      @B 16,0     ; above E:
        ;.BYTE 0,$FF,0,$7E, $01,$01,$7E,$80, $80,$7E,0,$FF, 0,0,0,0 ; TWO
        @B 16,$0
        @B 16,$0
        @B 16,$0    ; that's all in player 3
        @B 6,$0
        @B 4,$80
        @B 2,$C0
        @B 2,$E0
        @B 2,$F0
        @B 2,$F8
        @B 1,$FC
        @B 3,$FF
        @B 4,$7F
        @B 2,$1F
        @B 4,0
        @B 16,0
        @B 16,0     ; below E:
        *=  STARTPM+$380   ; START OF PLAYER 3
P3      @B 16,0     ; above E:
        ;.BYTE 0,$FF,0,$7E, $01,$01,$7E,$01, $01,$7E,0,$FF, 0,0,0,0 ; THREE
        @B 16,0     ; margin
        @B 4,0      ; last of 5 char margin
                    ; 4.5 chars of vertical line 4px wide
        @B 16,$F0
        @B 2,$F0
                    ; then start shifting over; 5px wide for 1 char, 6px wide for 1 char
        @B 4,$F8
        @B 4,$FC
                    ; stay at 6px wide, and shift over for 1 char, then shift over for one char
        @B 4,$7E
        @B 4,$3F
                    ; this is the point where Player2 starts
        @B 4,$1F
        @B 4,$0F
        @B 2,$03
        @B 4,$00   ; blank space while we switch sides and switch to size 3(8px)
        @B 1,$80
        @B 1,$C0
        @B 8,$F0
        @B 1,$30
        @B 1,0
        @B 16,0
        @B 16,0 ; below E:
        ; End of PM graphics data at $BC00
        .PAGE       Cartridge Trailer
        .IF TARGET = 2 .OR TARGET = 4 .OR TARGET = 8 ; any size cartridge
        *=  CARTINI
        ; Cart init code goes here -- usually nothing at all!
        RTS ; end of cart initialization
        .WORD STARTUP ; where to JMP after cart and OS are initted
        .BYTE 0     ; just a zero
        .BYTE 4     ; boot opt - no disk
        .WORD CARTINI
        .ENDIF
        .END
