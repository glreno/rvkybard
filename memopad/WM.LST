        .TITLE      WATERMARKED MEMO PAD
;
; All this thing does is boot up,
; set up a custom Display List,
; create a few PM graphics,
; and then JMP $E471,
; which is the built-in ATARI COMPUTER MEMO PAD application.
; This cartridge does NOT work on XL/XE computers!
;
        .OPT OBJ
        .PAGE       Addresses
BLKBDV  =   $E471
PMBASE  =   $D407
GRACTL  =   $D01D
SDMCTL  =   $022F
GPRIOR  =   $026F
HPOSP0  =   $D000
HPOSP1  =   $D001
HPOSP2  =   $D002
HPOSP3  =   $D003
HPOSM0  =   $D004
HPOSM1  =   $D005
HPOSM2  =   $D006
HPOSM3  =   $D007
SIZEP0  =   $D008
SIZEP1  =   $D009
SIZEP2  =   $D00A
SIZEP3  =   $D00B
SIZEM   =   $D00C
COLPM0  =   $02C0
COLPM1  =   $02C1
COLPM2  =   $02C2
COLPM3  =   $02C3
WSYNC   =   $D40A
VCOUNT  =   $D40B
VDLSTL  =   $0200 ; location of display list
VDLSTH  =   $0201
SDLSTL  =   $230
SDLSTH  =   $231
NMIEN   =   $D40E ; 0x80=DLI 0x40=VBI 0x20=RESET, default is 0x40

        .PAGE       Some handy macros
;
; @LC -- load a constant byte into an address
        .MACRO @LC
            LDA # %1
            STA %2
        .ENDM
;
; Put %1 copies of %2 into a .BYTE op
; "@B 4 $FF" instead of ".BYTE $FF $FF $FF $FF"
        .MACRO @B
            .IF %1 > 0
                .BYTE %2
                @B %1-1,%2
            .ENDIF
        .ENDM
;
        .PAGE       Cartridge Startup
        *=  $A000   ; 8K Cartridge
STARTUP
CR_DL   @LC 0,SDMCTL    ; turn off ANTIC while updating dlist
        ; copy dlist addr to page zero $CC
        LDA SDLSTL
        STA $CC
        LDA SDLSTH
        STA $CD
        LDY #3
        LDA ($CC),Y
        ORA #$80
        STA ($CC),Y
        INY
        INY
LOOP    INY
        LDA ($CC),Y
        CMP #2
        BNE EN_DL ; done, enable DLI
        ORA #$80
        STA ($CC),Y
        BNE LOOP ; let's just do one for now
EN_DL
        LDA #<DLI
        STA VDLSTL
        LDA #>DLI
        STA VDLSTH
EN_PM   @LC $B8,PMBASE
        @LC 3,GRACTL
        @LC 1,GPRIOR
        ; after dlist and pm are set up, enable Interrupts and ANTIC
        LDA #$C0
        STA NMIEN
        @LC $2E,SDMCTL  ; 2(norm playfield)+4(missiles)+8(players)+32(enable ANTIC)
; SET SIZES     $1C=00 01 11 00 -> 01=double-wide, 11=quad-wide
;       LDA #0
;       STA SIZEP0
;       STA SIZEP3
        LDA #1
        STA SIZEP1
        STA SIZEP2
        @LC $1C,SIZEM
; SET COLORS
        LDA #$84 ; DARK BLUE for the watermark (82 -- too dark, 8C -- too bright)
        ; LDA $2C ; ORANGE for P0
        STA COLPM0
        ; LDA $5C ; PINK for P1
        STA COLPM1
        ; LDA $8C ; BLUE for P2
        STA COLPM2
        ; LDA $BC ; GREEN for P3
        STA COLPM3
; Move them all in to place
;       @LC $6C,HPOSP0
        @LC $5C,HPOSP1
;       @LC $88,HPOSP3
        @LC $90,HPOSP2
        @LC $8E,HPOSM0 ; HPOSP3 + 6px orange
        @LC $7C,HPOSM1 ; was 80 pink
        @LC $78,HPOSM2 ; was 7C blue
        @LC $6C,HPOSM3 ; HPOSP0
        JMP BLKBDV  ; start memopad
;
;
;

        .PAGE       Display List Interrupt
;
;
DLI     PHA ; always save the accumulator!
        LDA VCOUNT
        CMP #19     ; second line (things are still invisible, no need to sync)
        BEQ ROW20
        CMP #23     ; third line
        BEQ ROW24
        CMP #79      ; 16th line
        BEQ ROW80
        ; All done with all the lines
        ;STA WSYNC ; wait for sync, without this the PM graphics go wonky when you type
DLDONE  PLA ; restore A
        RTI
        ;
        ; First row in the editor
        ; Set all sizes to 0 (normal) (1 is double-wide) (3 is quad-wide)
        ; Move to the left for debug
ROW20   @LC $14,$0614
        ;STA WSYNC ; no need to sync, all are invisible
        LDA #0
        STA SIZEP0
        STA SIZEP3
        JMP DLDONE
        ; Still above logo -- set all positions
ROW24   @LC $18,$0618
        @LC $6C,HPOSP0
;       @LC $5C,HPOSP1
        @LC $88,HPOSP3
;       @LC $90,HPOSP2
        ; STA WSYNC ; no need to sync, all are invisible
        JMP DLDONE
        ; the big one - double all sizes, and shift to new positions
ROW80   @LC $5C,$065C
        LDA #3
        STA SIZEP0
        STA SIZEP3
        @LC $3C,HPOSP0 ; HPOSP1-32
        @LC $A0,HPOSP3 ; HPOSP2+16
        ;STA WSYNC ; wait for sync, we're drawing here
        JMP DLDONE
        .PAGE       PM Image data
        ;
        ;
        ;
        *=  $B800   ; START OF PM DATA
        *=  $B980   ; START OF MISSILES
        ; Missiles are stored M3 M2 M1 M0
        ;          bit flags: C0 30 0C 03
        ; Missile 0 is on the RIGHT side of the screen with Player 3
        ; Missile 3 is on the LEFT side of the screen with Player 0
        ; Missiles 1&2 are in the center; 2 is quad size.
        ; The first four bytes (11,22,33,44) are off screen
        ;
        ;
M       @B 16,0                    ; above E:
        @B 16,0                    ; Each 16 bytes is 4 chars high; 5 char margin above image
        @B 4,0
        ; Vertical stripe on M2 M1: $3C = 00 11 11 00
        @B 12,$3C
        @B 16,$3C
        @B 16,$3C
        @B 2,$FF ; 1111 1111  
        @B 4,$BD ; 1011 1101   
        @B 9,$3C
        @B 1,0
        @B 16,$0                   ; 5 char margin to the end of E:
        @B 16,0                    ; below E:
        ; The last four bytes (11,22,33,44) are off screen
        *=  $BA00   ; START OF PLAYER 0
P0      @B 16,$0     ; above E:
        ;.BYTE 0,$FF,0,$7E, $81,$81,$81,$81, $81,$7E,0,$FF, 0,0,0,0 ; ZERO
        @B 16,0     ; margin
        @B 4,0      ; last of 5 char margin
                    ; 4.5 chars of vertical line 4px wide
        @B 16,$0F
        @B 2,$0F
                    ; then start shifting over; 5px wide for 1 char, 6px wide for 1 char
        @B 4,$1F
        @B 4,$3F
                    ; stay at 6px wide, and shift over for 1 char, then shift over for one char
        @B 4,$7E
        @B 4,$FC
                    ; this is the point where Player1 starts
        @B 4,$F8
        @B 4,$F0
        @B 2,$C0
        @B 4,$00   ; blank space while we switch sides and switch to size 3(8px)
        @B 1,$01
        @B 1,$03
        @B 8,$0F
        @B 1,$0C
        @B 1,0
        @B 16,0
        @B 16,0       ; below E:
        *=  $BA80   ; START OF PLAYER 1
P1      @B 16,$0    ; above E:
        ;.BYTE 0,$FF,0,$18, $38,$58,$18,$18, $18,$18,0,$FF, 0,0,0,0 ; ONE
        @B 16,$0
        @B 16,$0
        @B 16,$0    ; that's all in player 0
        @B 6,$0
        @B 4,$01
        @B 2,$03
        @B 2,$07
        @B 2,$0F
        @B 2,$1F
        @B 1,$3F
        @B 3,$FF
        @B 4,$FE
        @B 2,$F8
        @B 4,0
        @B 16,0
        @B 16,0     ; below E:
        *=  $BB00   ; START OF PLAYER 2
P2      @B 16,0     ; above E:
        ;.BYTE 0,$FF,0,$7E, $01,$01,$7E,$80, $80,$7E,0,$FF, 0,0,0,0 ; TWO
        @B 16,$0
        @B 16,$0
        @B 16,$0    ; that's all in player 3
        @B 6,$0
        @B 4,$80
        @B 2,$C0
        @B 2,$E0
        @B 2,$F0
        @B 2,$F8
        @B 1,$FC
        @B 3,$FF
        @B 4,$7F
        @B 2,$1F
        @B 4,0
        @B 16,0
        @B 16,0     ; below E:
        *=  $BB80   ; START OF PLAYER 3
P3      @B 16,0     ; above E:
        ;.BYTE 0,$FF,0,$7E, $01,$01,$7E,$01, $01,$7E,0,$FF, 0,0,0,0 ; THREE
        @B 16,0     ; margin
        @B 4,0      ; last of 5 char margin
                    ; 4.5 chars of vertical line 4px wide
        @B 16,$F0
        @B 2,$F0
                    ; then start shifting over; 5px wide for 1 char, 6px wide for 1 char
        @B 4,$F8
        @B 4,$FC
                    ; stay at 6px wide, and shift over for 1 char, then shift over for one char
        @B 4,$7E
        @B 4,$3F
                    ; this is the point where Player2 starts
        @B 4,$1F
        @B 4,$0F
        @B 2,$03
        @B 4,$00   ; blank space while we switch sides and switch to size 3(8px)
        @B 1,$80
        @B 1,$C0
        @B 8,$F0
        @B 1,$30
        @B 1,0
        @B 16,0
        @B 16,0 ; below E:
        ; End of PM graphics data at $BC00
        .PAGE       Cartridge Trailer
        *=  $BFF9
CARTINI RTS
        .WORD STARTUP
        .BYTE 0     ; just a zero
        .BYTE 4     ; boot opt - no disk
        .WORD CARTINI
        .END 
