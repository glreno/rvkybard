        .TITLE      MEMOPAD XL
;
; On bootup, JMP $A400, which is a clone of 
; the built-in ATARI COMPUTER MEMO PAD 
;
; To load into MAC65: ENTER #D:MPXL.LST,A
;
        .PAGE       SETUP
        .OPT OBJ
; Pick one of: 0=Cassette
;              1=Disk
;              2=2KB Cartridge
;              4=4KB Cartridge
;              8=8KB Cartridge
; (this is Line 200)
TARGET  = 8
;
; Where the assembled code goes!
        .IF TARGET = 8
STARTAP =   $A000 ; 8K left cartridge
BLACKBDSTART =   $A400
        .ENDIF
        .IF TARGET = 4
STARTAP =   $B000 ; 4K left cartridge
        .ENDIF
        .IF TARGET = 2
STARTAP =   $B800 ; 2K left cartridge
        .ENDIF
        .IF TARGET = 2 .OR TARGET = 4 .OR TARGET = 8 ; any size cartridge
CARTINI =   $BFF9 ; end of left cartridge
        .ENDIF
;
        .IF TARGET = 1 ; disk
; disk start vector ; this is line 400
STARTAP =   $2800 ; just above memlo after DOS loads
RUNAD   = $02E0
        *= RUNAD
        .WORD STARTUP
        .ENDIF

        .IF TARGET = 0 ; cassette
STARTAP =   $0700 ; 
CASINI  = $02
        *= CASINI
        .WORD STARTUP
        .ENDIF

        .PAGE       Addresses
;
; OS address constants
;
NOCLIK  =   $02DB ; alas, XL only
PMBASE  =   $D407
GRACTL  =   $D01D
SDMCTL  =   $022F
GPRIOR  =   $026F
HPOSP0  =   $D000
HPOSP1  =   $D001
HPOSP2  =   $D002
HPOSP3  =   $D003
HPOSM0  =   $D004
HPOSM1  =   $D005
HPOSM2  =   $D006
HPOSM3  =   $D007
SIZEP0  =   $D008
SIZEP1  =   $D009
SIZEP2  =   $D00A
SIZEP3  =   $D00B
SIZEM   =   $D00C
COLPM0  =   $02C0
COLPM1  =   $02C1
COLPM2  =   $02C2
COLPM3  =   $02C3
WSYNC   =   $D40A
VCOUNT  =   $D40B
VDLSTL  =   $0200 ; vector for display list interrupt
VDLSTH  =   $0201
SDLSTL  =   $0230 ; location of display list
SDLSTH  =   $0231
NMIEN   =   $D40E ; 0x80=DLI 0x40=VBI 0x20=RESET, default is 0x40
;
        .PAGE       Some handy macros
;
; @LC -- load a constant byte into an address
        .MACRO @LC
            LDA # %1
            STA %2
        .ENDM
;
; Put %1 copies of %2 into a .BYTE op
; "@B 4,$FF" instead of ".BYTE $FF $FF $FF $FF"
        .MACRO @B
            .IF %1 > 0
                .BYTE %2
                @B %1-1,%2
            .ENDIF
        .ENDM
;
;
        .PAGE       Application Startup
        *=  STARTAP
STARTUP ; @LC 1,NOCLIK ; Uncomment to turn of key click
;
;
;
;       Put your application here!
;
        JMP SIGNON  ; start memopad
CLS   =   $7D     ;CLEAR SCREEN CODE
CR  =   $9B     ; SYSTEM EOL (CARRIAGE RETURN)
IDENT:  .BYTE CLS,"ATARI COMPUTER - MEMO PAD",CR

; ------------------SNARFED FROM THE ATARI 800 OS --------------
; PUT LINE ON SCREEN AT PRESENT CURSOR POSITION
;
;    X-REG -- LO BYTE, BEGIN ADDR OF LINE
;    Y-REG -- HI BYTE, BEGIN ADDR OF LINE
;
ICBAL = $0344 ; IOCB buffer address low byte
ICBAH = $0345 ; IOCB buffer address high byte
ICBLL = $0348 ; IOCB buffer length low byte
ICCOM = $0342 ; IOCB command code
CIOV  = $E456 ; CIO utility entry point
PUTTXT =  $9  ;"PUT TEXT RECORD" CIO COMMANDCODE
PUTLIN: TXA
        LDX      #0           ; SCREEN EDITOR 10CR INDEX
        STA      ICBAL,X
        TYA
        STA      ICBAH,X      ;SET UP ADDR OF BEGIN OF LINE
        LDA      #PUTTXT
        STA      ICCOM,X      ;"PUT TEXT RECORD" COMMAND
        LDA      #$FF
        STA      ICBLL,X      ;SET BUFFER LENGTH
        JSR      CIOV          ;PUT LINE ON SCREEN
        RTS
;
;
; PRINT SIGN-ON MESSAGE
        *= BLACKBDSTART
SIGNON: LDX   #<IDENT
    LDY   #>IDENT
    JSR   PUTLIN  ;GO PUT SIGN-ON MSG ON SCREEN
;
;
;
;   BLACKBOARD ROUTINE
BLACKB: JSR   BLKB2   ;"JSR EGETCH"
    JMP   BLACKB  ;FOREVER
EDITRV=$E400
BLKB2:  LDA   EDITRV+5  ;HIGH BYTE
    PHA
    LDA   EDITRV+4  ;LOW BYTE
    PHA
    RTS       ;SIMULATES "JMP (EDITRV)"
;
; ---------------------------------- END SNARF
;

        .PAGE       Cartridge Trailer
        .IF TARGET = 2 .OR TARGET = 4 .OR TARGET = 8 ; any size cartridge
        *=  CARTINI
        ; Cart init code goes here -- usually nothing at all!
        RTS ; end of cart initialization
        .WORD STARTUP ; where to JMP after cart and OS are initted
        .BYTE 0     ; just a zero
        .BYTE 4     ; boot opt - no disk
        .WORD CARTINI
        .ENDIF
        .END
